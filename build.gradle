buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/repository/maven-public"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
    }
    dependencies {
        // classpath "com.github.RayDeeUx:ForgeGradle:6f5327"
        classpath "com.github.Skytils:ForgeGradle:6f5327"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
        classpath "com.github.xcfrg:MixinGradle:0.6-SNAPSHOT"
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "org.spongepowered.mixin"

version = "0.3.1"
group = "com.luna"
archivesBaseName = "Synthesis"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = "UTF-8"

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false

    clientJvmArgs += "-Delementa.dev=true"
    clientRunArgs += "--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker"
    clientRunArgs += "--mixin synthesis.mixins.json"
}

mixin {
    add sourceSets.main, "synthesis.mixins.refmap.json"
    defaultObfuscationEnv searge
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {
    mavenCentral()
    maven { url = "https://repo.sk1er.club/repository/maven-public/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
}

dependencies {
    embed "gg.essential:loader-launchwrapper:1.1.3"
    compileOnly "gg.essential:essential-1.8.9-forge:1759"

    annotationProcessor "org.spongepowered:mixin:0.8.4"
    compileOnly "org.spongepowered:mixin:0.8.4"

    annotationProcessor "com.google.code.gson:gson:2.2.4"
    annotationProcessor "com.google.guava:guava:21.0"
    annotationProcessor "org.ow2.asm:asm-tree:6.2"
    annotationProcessor "org.ow2.asm:asm-util:6.2"
    annotationProcessor "org.apache.logging.log4j:log4j-core:2.0-beta9"

    compileOnly "org.projectlombok:lombok:1.18.12"
    annotationProcessor "org.projectlombok:lombok:1.18.12"
}

shadowJar {
    archiveName = "Synthesis-NONCANON.jar"
    classifier = ""
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            "ForceLoadAsMod": true,
            "ModSide": "CLIENT",
            "TweakClass": "gg.essential.loader.stage0.EssentialSetupTweaker",
            "MixinConfigs": "synthesis.mixins.json"
    )

    exclude("dummyThing")
    exclude("META-INF/maven/")
    exclude("META-INF/nar/")
    exclude("module-info.class")
    exclude("META-INF/versions/")
    exclude("LICENSE.txt")

    configurations = [project.configurations.embed]
}

reobfJar.dependsOn tasks.shadowJar

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    from(file("LICENSE"))
}
